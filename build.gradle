plugins {
    id 'eclipse'
    id 'java-library'
    id 'maven-publish'
}

description="${project.name} build script"
group="org.fastily"
version="0.0.3"

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    api "org.fastily:jwiki:1.8.1"
}


java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    withJavadocJar()
    withSourcesJar()
}



javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.compileClasspath

    options {
        setMemberLevel JavadocMemberLevel.PUBLIC
        setAuthor true

        links "https://docs.oracle.com/en/java/javase/11/docs/api/"
        //links "https://square.github.io/okio/1.x/okio/"
        //links "https://square.github.io/okhttp/3.x/okhttp/"
        links "https://fastily.github.io/jwiki/docs/gson/"
        links "https://fastily.github.io/jwiki/docs/jwiki/"
        
        // links "http://square.github.io/okhttp/3.x/mockwebserver/"
        // setOutputLevel JavadocOutputLevel.VERBOSE
    }
}

wrapper {
    gradleVersion = '6.5.1'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = 'wp-toolbox'
                description = 'Shared functionality for Fastily\'s Wikipedia tools and bots'
                inceptionYear = '2018'
                url = 'https://github.com/fastily/wp-toolbox'
                licenses {
                    license {
                        name = 'GNU General Public License, Version 3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'fastily'
                        name = 'Fastily'
                        email = 'fastily@users.noreply.github.com'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/fastily/wp-toolbox.git'
                    developerConnection = 'scm:git:ssh://github.com/fastily/wp-toolbox.git'
                    url = 'https://github.com/fastily/wp-toolbox'
                }
            }
        }
    }
}


task wgen(type: Jar) {

    archiveClassifier = 'uber'

    archiveBaseName = "WGen"

    from sourceSets.main.output

    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }

    manifest {
        attributes 'Main-Class': 'org.fastily.wptoolbox.WGen'
    }

    exclude "META-INF/*.SF", "**/LICENSE*", "**/NOTICE*", "**/maven/**"
}