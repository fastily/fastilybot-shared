plugins {
    id 'java-library'
    id 'maven-publish'
    id 'eclipse'
}

version="0.0.2"
description="${project.name} build script"
group="fastily"

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11


dependencies {
    api "fastily:jwiki:1.7.1"
}

repositories {
    mavenLocal()
    jcenter()
}

javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.compileClasspath

    options {
        setMemberLevel JavadocMemberLevel.PUBLIC
        setAuthor true

        links "https://docs.oracle.com/en/java/javase/11/docs/api/"
        links "https://square.github.io/okio/1.x/okio/"
        links "https://square.github.io/okhttp/3.x/okhttp/"
        links "https://fastily.github.io/jwiki/docs/gson/"
        links "https://fastily.github.io/jwiki/docs/jwiki/"
        
        // links "http://square.github.io/okhttp/3.x/mockwebserver/"
        // setOutputLevel JavadocOutputLevel.VERBOSE
    }
}


task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task packageJavadoc(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

wrapper {
    gradleVersion = '5.6.3'
}

publishing {
    publications {
        mavenPublish(MavenPublication) {
            from components.java

            artifact sourceJar
            artifact packageJavadoc

            pom {
                name = 'wp-toolbox'
                description = "Shared functionality for Fastily's Wikipedia tools and bots"
                inceptionYear = '2018'
                url = 'https://github.com/fastily/wp-toolbox'
                licenses {
                    license {
                        name = 'GNU General Public License, Version 3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
                        distribution = 'repo'
                    }
                }
            }
        }
    }
}

task wgen(type: Jar) {
  baseName = "WGen"

  from {
    configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
  }

  manifest {
    attributes 'Main-Class': 'fastily.wptoolbox.WGen'
  }
  
  exclude "META-INF/*.SF", "**/LICENSE*", "**/NOTICE*", "**/maven/**"

  with jar
}